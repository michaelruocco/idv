plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.bmuschko.docker-remote-api' version '6.4.0'
    id 'com.avast.gradle.docker-compose' version '0.10.10'
    id 'com.moowork.node' version '1.3.1'
    id 'com.github.michaelruocco.gradle-postman-runner' version '0.1.2'
    id 'org.unbroken-dome.test-sets' version "${testSetsPluginVersion}"
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

testSets {
    integrationTest
}

repositories {
    mavenCentral()
}

ext['junit-jupiter.version'] = ext.junitVersion

configurations {
    testCompile.exclude module: 'slf4j-simple'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-dependencies:2.2.6.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation project(':uk-config')

    implementation project(':verification-context-entities')
    implementation project(':verification-context-use-cases')

    implementation project(':verification-context-dynamo-db')
    implementation project(':verification-context-in-memory-db')
    implementation project(':verification-context-redis-db')

    implementation project(':one-time-passcode-entities')
    implementation project(':one-time-passcode-use-cases')

    implementation project(':one-time-passcode-dynamo-db')
    implementation project(':one-time-passcode-in-memory-db')

    implementation project(':one-time-passcode-sns-delivery')
    implementation project(':one-time-passcode-in-memory-delivery')

    testImplementation testFixtures(project(':verification-context-entities'))
    testImplementation testFixtures(project(':verification-context-use-cases'))
    testImplementation project(':verification-context-api').sourceSets.test.output
    testImplementation testFixtures(project(':one-time-passcode-entities'))
    testImplementation project(':one-time-passcode-use-cases').sourceSets.test.output

    integrationTestImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    integrationTestImplementation project(':one-time-passcode-sns-delivery').sourceSets.integrationTest.output
    integrationTestImplementation testFixtures(project(':verification-context-redis-db'))
    integrationTestImplementation "org.testcontainers:junit-jupiter:${testContainersVersion}"
}

integrationTest {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

bootRun {
    systemProperties = System.properties
    args = ["--server.port=8081"]
}

docker {
    registryCredentials {
        username = System.getenv("DOCKER_USERNAME")
        password = System.getenv("DOCKER_PASSWORD")
    }
}

task buildImage(type: DockerBuildImage) {
    inputDir.set(file('.'))
    images.add('michaelruocco/verification-context-spring-app:latest')
}

task pushImage(type: DockerPushImage) {
    images.set(buildImage.images)
}

node {
    download = false
}

postman {
    collections = fileTree(dir: '../postman', include: '*.postman_collection.json')
    environment = file('../postman/local.postman_environment.json')
}