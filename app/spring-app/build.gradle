plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.bmuschko.docker-remote-api' version '6.1.4'
    id 'com.avast.gradle.docker-compose' version '0.10.9'
    id 'com.moowork.node' version '1.3.1'
    id 'com.github.michaelruocco.gradle-postman-runner' version '0.1.2'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

testSets {
    integrationTest
}

repositories {
    mavenCentral()
}

ext['junit-jupiter.version'] = ext.junitVersion

dependencies {
    implementation 'org.springframework.boot:spring-boot-dependencies:2.2.5.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    compile project(':entities')
    compile project(':use-cases')
    compile project(':api')

    compile project(':dynamo-db')
    compile project(':in-memory-db')
    compile project(':uk-config')
    compile project(':in-memory-sender')

    testCompile project(':entities').sourceSets.test.output
    testCompile project(':use-cases').sourceSets.test.output
    testCompile project(':api').sourceSets.test.output

    integrationTestCompile 'com.github.stefanbirkner:system-rules:1.19.0'
    integrationTestCompile project(':dynamo-db').sourceSets.integrationTest.output
    integrationTestCompile "org.testcontainers:junit-jupiter:${testContainersVersion}"
}

bootRun {
    args = ["--server.port=8081"]
}

docker {
    registryCredentials {
        username = System.getenv("DOCKER_USERNAME")
        password = System.getenv("DOCKER_PASSWORD")
    }
}

task buildImage(type: DockerBuildImage) {
    inputDir.set(file('.'))
    images.add('michaelruocco/verification-context-spring-app:latest')
}

task pushImage(type: DockerPushImage) {
    images.set(buildImage.images)
}

node {
    download = false
}

postman {
    collections = fileTree(dir: '../postman', include: '*postman_collection.json')
    environment = file('../postman/local.postman_environment.json')
}