plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.9.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.diffplug.gradle.spotless' version '3.24.3'
    id 'com.github.ben-manes.versions' version '0.25.0'
}

group 'uk.co.mruoc'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-dependencies:2.1.9.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    
    compileOnly 'org.projectlombok:lombok:1.18.10'
    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'

    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'org.javamoney:moneta:1.3'
    compile 'org.zalando:jackson-datatype-money:1.1.1'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.0'
    compile 'org.springframework.boot:spring-boot-starter-hateoas:2.1.4.RELEASE'
    compile 'org.apache.commons:commons-collections4:4.4'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

    testCompile 'org.assertj:assertj-core:3.13.2'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:3.1.10'
    testCompile 'com.github.michaelruocco:file-loader:5.0.1'
    testCompile 'net.javacrumbs.json-unit:json-unit-assertj:2.9.0'

    testCompile ('org.mockito:mockito-core:3.1.0') {
        //mockito 3 is failing with version of byte buddy it brings automatically?!
        //this line excludes it and then latest version is included below
        exclude group: 'net.bytebuddy'
    }
    testCompile 'net.bytebuddy:byte-buddy:1.10.1'

}

test {
    useJUnitPlatform()
}

bootJar {
    mainClassName = 'uk.co.mruoc.idv.verificationcontext.Application'
}

spotless {
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()

        replace      'Not enough space after if', 'if(', 'if ('
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('

        replace      'Not enough space after for', 'for(', 'for ('
        replaceRegex 'Too much space after for', 'for +\\(', 'for ('

        replace      'Not enough space after while', 'while(', 'while ('
        replaceRegex 'Too much space after while', 'while +\\(', 'while ('
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}