plugins {
    id 'java'
    id 'idea'
    id 'com.diffplug.gradle.spotless' version '3.26.1'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.vanniktech.dependency.graph.generator' version '0.5.0'
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    jacoco {
        toolVersion = '0.8.5'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.gradle.spotless'

    group 'com.github.michaelruocco'
    version '1.0.0-SNAPSHOT'
    archivesBaseName = "${rootProject.name}-${project.name}"

    sourceCompatibility = 11
    targetCompatibility = 11

    ext {
        junitVersion = '5.5.2'
        lombokVersion = '1.18.10'
        slf4jVersion = '1.7.29'
        meanBeanVersion = '2.0.3'
        equalsVerifierVersion = '3.1.10'
        fileLoaderVersion = '5.0.1'
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testCompile 'org.assertj:assertj-core:3.14.0'
        testCompile 'org.mockito:mockito-core:3.2.0'
    }

    test {
        useJUnitPlatform()
    }

    spotless {
        java {
            removeUnusedImports()
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()

            replace 'Not enough space after if', 'if(', 'if ('
            replaceRegex 'Too much space after if', 'if +\\(', 'if ('

            replace 'Not enough space after for', 'for(', 'for ('
            replaceRegex 'Too much space after for', 'for +\\(', 'for ('

            replace 'Not enough space after while', 'while(', 'while ('
            replaceRegex 'Too much space after while', 'while +\\(', 'while ('
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

import com.vanniktech.dependency.graph.generator.DependencyGraphGeneratorExtension.Generator

def idvGenerator = new Generator(
        "idv",
        { dependency -> dependency.getModuleGroup().startsWith("com.github.michaelruocco") },
        { dependency -> true }
)

dependencyGraphGenerator {
    generators = [Generator.ALL, idvGenerator]
}